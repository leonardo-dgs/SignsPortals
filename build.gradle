import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
}

group = 'net.leonardo_dgs'
version = '1.1.5'
description = 'Let users create their own portals with signs!'

String mainPackage = project.group + '.' + project.name.toLowerCase()
String apiVersion = '1.13'
String author = 'Leonardo_DGS'
String website = 'https://www.spigotmc.org/resources/55287/'

String spigotVersion = '1.16.1-R0.1-SNAPSHOT'
String bungeeVersion = '1.16-R0.1'
String vaultVersion = '1.7'
String lombokVersion = '1.18.12'
String bStatsVersion = '1.5'
String idbVersion = '1.0.0-SNAPSHOT'
String hikariVersion = '3.4.5'
String slf4jVersion = '1.7.30'
String helperVersion = '5.6.2'

tasks.compileJava.sourceCompatibility = 1.8
tasks.compileJava.targetCompatibility = 1.8
tasks.compileJava.options.encoding = 'UTF-8'

repositories {
    jcenter()

    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://repo.codemc.org/repository/maven-public/' }
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
    maven { url = 'https://jitpack.io' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:' + spigotVersion
    compileOnly 'net.md-5:bungeecord-chat:' + bungeeVersion
    compileOnly 'com.github.MilkBowl:VaultAPI:' + vaultVersion
    compileOnly 'org.projectlombok:lombok:' + lombokVersion
    annotationProcessor 'org.projectlombok:lombok:' + lombokVersion
    implementation 'org.bstats:bstats-bukkit-lite:' + bStatsVersion
    implementation 'co.aikar:idb-core:' + idbVersion
    implementation 'com.zaxxer:HikariCP:' + hikariVersion
    implementation 'org.slf4j:slf4j-api:' + slf4jVersion
    implementation 'org.slf4j:slf4j-nop:' + slf4jVersion
    implementation 'me.lucko:helper:' + helperVersion
}

tasks.processResources {
    filter ReplaceTokens, tokens: [
            "name"       : project.name,
            "mainPackage": mainPackage,
            "version"    : project.version,
            "apiVersion" : apiVersion,
            "description": project.description,
            "author"     : author,
            "website"    : website
    ]
}

tasks.jar.enabled = false

tasks.shadowJar {
    archiveFileName = project.name + '.jar'
    minimize()
    relocate 'org.bstats.bukkit', mainPackage + '.bstats'
    relocate 'me.lucko.helper', mainPackage + '.helper'
    relocate 'co.aikar.idb', mainPackage + '.idb'
    relocate 'com.zaxxer.hikari', mainPackage + '.hikari'
    relocate 'org.slf4j', mainPackage + '.slf4j'
}

artifacts {
    archives shadowJar
}

configurations.all {
    transitive = false
}