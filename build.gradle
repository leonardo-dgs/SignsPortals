import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '5.0.0'
}

group = 'net.leomixer17'
version = '1.1.3-SNAPSHOT'
description = 'Let users create their own portals with signs!'

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.compileJava.options.encoding = 'UTF-8'

repositories {
    jcenter()
    mavenCentral()

    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url = 'http://nexus.hc.to/content/repositories/pub_releases' }
    maven { url = 'https://jitpack.io' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.13.2-R0.1-SNAPSHOT'
    compileOnly 'net.md-5:bungeecord-chat:1.13-SNAPSHOT'
    compileOnly 'net.milkbowl.vault:VaultAPI:1.7'
    compile 'com.github.Leomixer17.PluginLib:bukkit:-SNAPSHOT'
}

tasks.processResources {
    filter ReplaceTokens, tokens: [
            "version"    : project.property("version"),
            "description": project.property("description")
    ]
}

tasks.jar.enabled = false

tasks.shadowJar {
    archiveFileName = 'SignsPortals.jar'
    minimize()
    relocate 'net.leomixer17.pluginlib', 'net.leomixer17.signsportals'
}

artifacts {
    archives shadowJar
}

configurations.all {
    transitive = false
}
