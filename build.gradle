plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group = 'net.leonardo_dgs'
version = '1.1.5'
description = 'Let users create their own portals with signs!'

def mainPackage = 'net.leonardo_dgs.signsportals'

tasks.compileJava.sourceCompatibility = JavaVersion.VERSION_1_8
tasks.compileJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()

    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://repo.codemc.org/repository/maven-public/' }
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
    maven { url = 'https://jitpack.io/' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.17.1-R0.1-SNAPSHOT'
    compileOnly 'net.md-5:bungeecord-chat:1.17-R0.1-SNAPSHOT'
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'
    implementation 'org.bstats:bstats-bukkit:2.2.1'
    implementation 'com.github.simplix-softworks:SimplixStorage:3.2.3'
    implementation 'co.aikar:idb-core:1.0.0-SNAPSHOT'
    implementation 'com.zaxxer:HikariCP:5.0.0'
    implementation 'org.slf4j:slf4j-api:1.7.32'
    implementation 'org.slf4j:slf4j-nop:1.7.32'
}

processResources {
    expand(
            version: project.version,
            description: project.description,
            author: 'Leonardo_DGS',
            website: 'https://www.spigotmc.org/resources/55287/'
    )
}

tasks.jar.enabled = false

tasks.shadowJar {
    archiveBaseName.set(project.name)
    archiveClassifier.set("")
    archiveVersion.set("")
    minimize()

    relocate 'org.bstats.bukkit', mainPackage + '.bstats'
    relocate 'de.leonhard.storage', mainPackage + '.config'
    relocate 'co.aikar.idb', mainPackage + '.idb'
    relocate 'com.zaxxer.hikari', mainPackage + '.hikari'
    relocate 'org.slf4j', mainPackage + '.slf4j'
}

artifacts {
    archives shadowJar
}
