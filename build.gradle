import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

group = 'net.leonardo_dgs'
version = '1.1.5'
description = 'Let users create their own portals with signs!'

String mainPackage = project.group + '.' + project.name.toLowerCase()
String apiVersion = '1.13'
String author = 'Leonardo_DGS'
String website = 'https://www.spigotmc.org/resources/signsportals.55287/'

String spigotVersion = '1.15.2-R0.1-SNAPSHOT'
String bungeeVersion = '1.15-SNAPSHOT'
String vaultVersion = '1.7'
String lombokVersion = '1.18.12'
String bStatsVersion = '1.5'
String helperVersion = '5.6.2'

tasks.compileJava.sourceCompatibility = 1.8
tasks.compileJava.targetCompatibility = 1.8
tasks.compileJava.options.encoding = 'UTF-8'

repositories {
    jcenter()

    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url = 'https://repo.codemc.org/repository/maven-public/' }
    maven { url = 'https://jitpack.io' }
}

dependencies {
    shadow 'org.spigotmc:spigot-api:' + spigotVersion
    shadow 'net.md-5:bungeecord-chat:' + bungeeVersion
    shadow 'com.github.MilkBowl:VaultAPI:' + vaultVersion
    shadow 'org.projectlombok:lombok:' + lombokVersion
    annotationProcessor 'org.projectlombok:lombok:' + lombokVersion
    implementation 'org.bstats:bstats-bukkit-lite:' + bStatsVersion
    implementation 'me.lucko:helper:' + helperVersion
}

tasks.processResources {
    filter ReplaceTokens, tokens: [
            "name"       : project.name,
            "mainPackage": mainPackage,
            "version"    : project.version,
            "apiVersion" : apiVersion,
            "description": project.description,
            "author"     : author,
            "website"    : website
    ]
}

tasks.jar.enabled = false

tasks.shadowJar {
    archiveFileName = project.name + '.jar'
    minimize()
    relocate 'me.lucko.helper', mainPackage + ".helper"
    relocate 'org.bstats.bukkit', mainPackage + '.bstats'
}

artifacts {
    archives shadowJar
}

configurations.all {
    transitive = false
}